#INCLUDE "PROTHEUS.CH"

/*

Ŀ
Programa  CARGACF    Autor  J.Donizete R.Silva     Data 13/10/2006
Ĵ
Descricao  Este programa se destina a clientes que mentm uma conta   
           contbil para cada Cliente/Fornecedor. Este programa l o  
           cadastro de Clientes e Fornecedores e cria as respectivas  
           contas com base em critrios definidos pelo contador da    
           empresa.                                                   
Ĵ
Parametros -                                                          
Ĵ
Retorno    -                                                          
Ĵ
Aplicacao  -                                                          
Ĵ
Uso        Chamada atravs do menu em Miscelnea.                     
Ĵ
Analista Resp.  Data   Bops  Manutencao Efetuada                    
Ĵ
                /  /                                                
                /  /                                                
ٱ

*/

User Function CARGACF()

// Variaveis da funo
Local oRadioGrp1
Public  _nOP	:= 0
Private _oDlg	// Dialog Principal

DEFINE MSDIALOG _oDlg TITLE "Carga de Clientes e Fornecedores no Plano de Contas" FROM C(247),C(330) TO C(401),C(662) PIXEL

// Cria Componentes Padroes do Sistema
@ C(004),C(004) TO C(058),C(165) LABEL "Opes" PIXEL OF _oDlg
@ C(008),C(007) Radio oRadioGrp1 Var _nOp Items "Clientes","Fornecedores","Ambos" 3D Size C(090),C(010) PIXEL OF _oDlg
@ C(062),C(082) Button "Processar" Size C(037),C(012) PIXEL OF _oDlg Action(Processa({|| OkProc() },"Processando..."),_oDlg:End())
@ C(062),C(124) Button "Cancelar" Size C(037),C(012) PIXEL OF _oDlg Action(_oDlg:End())

ACTIVATE MSDIALOG _oDlg CENTERED   

Return(.T.)


/*

Ŀ
Programa   OkProc    Autores                          Data 13/10/2006
Ĵ
Descricao   Funcao responsavel pelo processamento principal da rotina.   
                                                                         
ٱ

*/
Static Function OkProc()

// Verifica se o mdulo em uso  o SIGACTB.
If Alltrim(GETMV("MV_MCONTAB")) <> "CTB"
	MsgStop("Esta rotina se aplica somente ao mdulo contbil SIGACTB!")
	Return
EndIf

// Faz a chamada de processamento.
If _nOp=0
	MsgAlert("Nenhuma opo escolhida!")
	Return
ElseIf _nOp=1 // Clientes
	ProcSA1()
ElseIf _nOp=2 // Fornecedores
	ProcSA2()
ElseIf _nOp=3 // Ambos
	ProcSA1() // Clientes
	ProcSA2() // Fornecedores
EndIf

Return

/*

Ŀ
Programa   PROCSA1   Autores  J.Donizete R.Silva      Data 13/10/2006
Ĵ
Descricao   Funcao responsavel por criar as contas de clientes.          
                                                                         
ٱ

*/
Static Function ProcSA1()

// Variaveis da funo
Local _cNome		:= ""
Local _cCod			:= ""
Local _cLoja		:= ""
Local _cCta			:= ""
Local _aCad			:= {}
Local lMsErroAuto 	:= .F.
Local lMsHelpAuto 	:= .T.

// Abre o alias a processar.
dbSelectArea("SA1")
dbSetOrder(1)
DbGotop()
ProcRegua(SA1->(RecCount()))

// Processa o cadastro de clientes.
While !Eof()
	
	_cNome	:= SA1->A1_NOME
	_cCod	:= SA1->A1_COD+SA1->A1_LOJA
	_cCta	:= ""

	// Reseta matriz.
	_aCad	:= {}
	
	IncProc("Processando Cliente: " + _cCod)
	
	// Este escopo deve ser atualizado com as regras definidas pelo Contador.
	If SA1->A1_EST == "EX"
		_cCta := "11020102"
	ElseIf SA1->A1_EST <> "EX" .And. !Empty(SA1->A1_EST)
		_cCta := "11020101"
	Else
		_cCta := ""
	EndIf
	
	// Verifica se tem conta a ser processada.
	If !Empty(_cCta)
		
		dbSelectArea("CT1")
		dbSetOrder(1)
		
		// Caso no encontre a conta no plano de contas, criar a mesma.
		If !DbSeek(xFilial("CT1") + _cCta + _cCod)
			
			aAdd( _aCad , { "CT1_FILIAL"  , xFilial("CT1") , Nil } )
			aAdd( _aCad , { "CT1_CONTA"   , _cCta+_cCod    , Nil } )
			aAdd( _aCad , { "CT1_DESC01"  , _cNome         , Nil } )
			aAdd( _aCad , { "CT1_CLASSE"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_NORMAL"  , "1"            , Nil } )
			aAdd( _aCad , { "CT1_RES"     , _cCod          , Nil } )
			//aAdd( _aCad , { "CT1_CTASUP"  , _cCta          , Nil } )
			aAdd( _aCad , { "CT1_ACCUST"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_ACITEM"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_ACCLVL"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_CCOBRG"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_ITOBRG"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_CLOBRG"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_BOOK"    , "001/002/003/004/005"  , Nil } )
			aAdd( _aCad , { "CT1_RGNV1"   , "N"            , Nil } )
			
			
			// Inclui a conta contbil atravs de rotina automtica.
			MSExecAuto({|x,y| CTBA020(x,y)},_aCad,3) //Inclusao
			If lMsErroAuto
				MostraErro()
				Alert("No foi possvel incluir registro.")
			Endif
			
		EndIf
		
		// Atualiza o cadastro de clientes, independente de ter criado a conta.
		DbSelectArea("SA1")
		If Empty(SA1->A1_CONTA)
			If Reclock("SA1", .F.)
				SA1->A1_CONTA	:= _cCta+_cCod
				MsUnlock()
			EndIf
		EndIf
	EndIf
	
	// Processa o prximo cliente.
	DbSelectArea("SA1")
	dbSkip()
Enddo
Return

/*

Ŀ
Programa   PROCSA2   Autores  J.Donizete R.Silva      Data 13/10/2006
Ĵ
Descricao   Funcao responsavel por criar as contas de fornecedores.      
                                                                         
ٱ

*/
Static Function ProcSA2()

// Variaveis da funo
Local _cNome		:= ""
Local _cCod			:= ""
Local _cLoja		:= ""
Local _cCta			:= ""
Local _aCad			:= {}
Local lMsErroAuto 	:= .F.
Local lMsHelpAuto 	:= .T.

// Abre o alias a processar.
dbSelectArea("SA2")
dbSetOrder(1)
DbGotop()
ProcRegua(SA2->(RecCount()))

// Processa o cadastro de fornecedores.
While !Eof()
	
	_cNome	:= SA2->A2_NOME
	_cCod	:= SA2->A2_COD+SA2->A2_LOJA
	_cCta	:= ""

	// Reseta matriz.
	_aCad	:= {}
	
	IncProc("Processando Fornecedor: " + _cCod)
	
	// Este escopo deve ser atualizado com as regras definidas pelo Contador.
	If SubStr(SA2->A2_COD,1,1) == "0"
		_cCta := "21010101"+_cCod
	ElseIf SubStr(SA2->A2_COD,1,1) == "F"
		_cCta := "21010509"
	ElseIf SubStr(SA2->A2_COD,1,1) == "O"
		_cCta := "21010509"
	ElseIf SubStr(SA2->A2_COD,1,1) == "V"
		_cCta := "21010504"
	Else
		_cCta := ""
	EndIf
	
	// Verifica se tem conta a ser processada.
	If !Empty(_cCta)
		
		dbSelectArea("CT1")
		dbSetOrder(1)
		
		// Caso no encontre a conta no plano de contas, criar a mesma.
		If !DbSeek(xFilial("CT1") + _cCta)
			
			aAdd( _aCad , { "CT1_FILIAL"  , xFilial("CT1") , Nil } )
			aAdd( _aCad , { "CT1_CONTA"   , _cCta          , Nil } )
			aAdd( _aCad , { "CT1_DESC01"  , _cNome         , Nil } )
			aAdd( _aCad , { "CT1_CLASSE"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_NORMAL"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_RES"     , _cCod          , Nil } )
			//aAdd( _aCad , { "CT1_CTASUP"  , _cCta          , Nil } )
			aAdd( _aCad , { "CT1_ACCUST"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_ACITEM"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_ACCLVL"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_CCOBRG"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_ITOBRG"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_CLOBRG"  , "2"            , Nil } )
			aAdd( _aCad , { "CT1_BOOK"    , "001/002/003/004/005"  , Nil } )
			aAdd( _aCad , { "CT1_RGNV1"   , "N"            , Nil } )
			
			
			// Inclui a conta contbil atravs de rotina automtica.
			CTBA020( _aCad , , 3)
			If lMsErroAuto
				MostraErro()
				Alert("No foi possvel incluir registro.")
			Endif
			
		EndIf
		
		// Atualiza o cadastro de clientes, independente de ter criado a conta.
		DbSelectArea("SA2")
		If Empty(SA2->A2_CONTA)
			If Reclock("SA2", .F.)
				SA2->A2_CONTA	:= _cCta
				MsUnlock()
			EndIf
		EndIf
	EndIf
	
	// Processa o prximo fornecedor.
	DbSelectArea("SA2")
	dbSkip()
Enddo
Return

/*

Ŀ
Programa      C()    Autores                          Data 10/05/2005
Ĵ
Descricao   Funcao responsavel por manter o Layout independente da       
            resolucao horizontal do Monitor do Usuario.                  
ٱ

*/
Static Function C(nTam)
Local nHRes	:=	oMainWnd:nClientWidth	// Resolucao horizontal do monitor
If nHRes == 640	// Resolucao 640x480 (soh o Ocean e o Classic aceitam 640)
	nTam *= 0.8
ElseIf (nHRes == 798).Or.(nHRes == 800)	// Resolucao 800x600
	nTam *= 1
Else	// Resolucao 1024x768 e acima
	nTam *= 1.28
EndIf

//Ŀ
//Tratamento para tema "Flat"
//
If "MP8" $ oApp:cVersion
	If (Alltrim(GetTheme()) == "FLAT") .Or. SetMdiChild()
		nTam *= 0.90
	EndIf
EndIf
Return Int(nTam)
